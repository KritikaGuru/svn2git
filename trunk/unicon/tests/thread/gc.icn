#
# Long run program that is garbage collection intensive
#

global gsum
procedure main(argv)
   if not (&features == "concurrent threads") then
      stop("This program requires concurrent threads.")

   gsum := 0
   thrd := []

   if *argv>0 then
      n := integer(argv[1]) | stop ("arg must be integer")
   else
      n := 1

   d := 10^8 / n
   write("running ", n , " thread(s) please wait... ")
   t := &now
   every i := !n do put(thrd, thread suma(d, i))
   every wait(!thrd)

   write("sum=", gsum)
   write("time:", &now-t, " seconds")
end


procedure suma(n, id)
   local sum := 0, L, T, S
   static region
   initial region := mutex()

   every i := 1 to n do {
      sum +:= 1
      if i%100 = 1  then {
         L := []
         T := table()
         S := set()
	 }

      put(L, i)
      T[i] := sum
      insert(S, i, sum)
      }

   critical region: gsum +:= sum
   write("Thread ", id , " is done")
end
